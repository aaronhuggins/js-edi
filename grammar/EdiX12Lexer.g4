lexer grammar EdiX12Lexer;

@lexer::header {
// This statement may be removed once option `superClass` is correctly generated by antlr4ts.
import { X12BaseLexer } from "./X12BaseLexer";
}

options {
  superClass=X12BaseLexer;
}

tokens {
  DataSeparator,
  RepititionSeparator,
  ComponentSeparator,
  SegmentTerminator,
  EndOfLine
}

// Fragments for non-printable character detection.
fragment SOH: '\u0001';
fragment STX: '\u0002';
fragment ETX: '\u0003';
fragment EOT: '\u0004';
fragment ENQ: '\u0005';
fragment ACK: '\u0006';
fragment BEL: '\u0007';
fragment HT: '\u0009';
fragment LF: '\u000A';
fragment VT: '\u000B';
fragment FF: '\u000C';
fragment CR: '\u000D';
fragment DC1: '\u0011';
fragment DC2: '\u0012';
fragment DC3: '\u0013';
fragment DC4: '\u0014';
fragment NAK: '\u0015';
fragment SYN: '\u0016';
fragment ETB: '\u0017';
fragment FS: '\u001C';
fragment GS: '\u001D';
fragment RS: '\u001E';
fragment US: '\u001F';
fragment AsciiControl:
    SOH | STX
  | ETX | EOT
  | ENQ | ACK
  | BEL | HT
  | LF | VT
  | FF | CR
  | DC1 | DC2
  | DC3 | DC4
  | NAK | SYN
  | ETB | FS
  | GS | RS
  | US;

// Fragments for printable character detection.
fragment Number: [0-9];
fragment Letter: [a-zA-Z];
fragment Special: [ !"&'()*+,-./:;?=%~@[\]_{}\\|<>#$];

// Special token for discovering the position of control characters for a given EDI document.
ControlChar:
    Special { this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) }
  | CR LF { this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) }
  | CR { this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) }
  | LF { this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) }
  | AsciiControl { this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) };

// All valid printable characters.
Char:
    Letter
  | Number
  | Special { !this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) };

// Reserved segment tags.
InterchangeHeader: 'ISA';
InterchangeTrailer: 'IEA';
GroupHeader: 'GS';
GroupTrailer: 'GE';
TransactionHeader: 'ST';
TransactionTrailer: 'SE';

// All other segment tags.
Tag: Letter (Letter|Number) (Letter|Number)*;

// All non-printable and formatting marks
NonPrintable:
    CR LF { !this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) }
  | AsciiControl { !this.isCtlCharPos }? { this.handleControlChars(EdiX12Lexer) };
